buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.squareup.sqldelight:gradle-plugin:1.3.0'
    }
}

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

apply plugin: 'kotlin-kapt'

group 'com.aglushkov'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

javafx {
    version = "14"
    modules = [ 'javafx.controls', 'javafx.fxml']
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/java"]
            includes = ["**/*.fxml", "**/*.bin"]
        }
    }
}

mainClassName = "com.aglushkov.nlphelper.app.MainApp"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.6"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.3.6"
    implementation group: 'org.apache.opennlp', name: 'opennlp-tools', version: '1.9.2'

    implementation "com.squareup.sqldelight:sqlite-driver:1.3.0"

    implementation 'com.google.dagger:dagger:2.27'
    kapt 'com.google.dagger:dagger-compiler:2.27'

    implementation 'junit:junit:4.13'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.6'

}

jar {
    manifest {
        attributes 'Main-Class': 'org.openjfx.nlphelper.Launcher'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

apply plugin: 'com.squareup.sqldelight'

sqldelight {
    // Database name
    Database {
        // Package name used for the generated MyDatabase.kt
        packageName = "com.aglushkov.nlphelper"

        // An array of folders where the plugin will read your '.sq' and '.sqm'
        // files. The folders are relative to the existing source set so if you
        // specify ["db"], the plugin will look into 'src/main/db'.
        // Defaults to ["sqldelight"] (src/main/sqldelight)
        //sourceFolders = ["sqldelight"]

        // The directory where to store '.db' schema files relative to the root
        // of the project. These files are used to verify that migrations yield
        // a database with the latest schema. Defaults to null so the verification
        // tasks will not be created.
        schemaOutputDirectory = file("src/main/java/com/aglushkov/db")

        // Optionally specify schema dependencies on other gradle projects
        //dependency project(':OtherProject')
    }

    // For native targets, whether sqlite should be automatically linked.
    // Defaults to true.
    // linkSqlite = false
}